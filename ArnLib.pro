PROJECT = ArnLib

# ARN += core       # Level 1: Basic Arn functionality without any tcp and syncing
# ARN += client     # Level 2: Client TCP functionality with sync etc
# ARN += server     # Level 3: Server TCP functionality with persistence etc
# ARN += scriptjs   # Java script support QT >= 5 (new QJSEngine)
# ARN += script     # Java script support QT <= 5 (legacy QtScriptEngine)
# ARN += scriptauto # Java script support depend on Qt version: scriptjs (prefered) or script
# ARN += qml        # QML support
# ARN += zeroconf   # Using part of Bonjour (R), Apple's (R) implementation of zero-configuration networking.
# ARN += discover   # High level service discovery using <zeroconf> and optionally <server> for remote config.
#
# CONFIG += ArnRealFloat   # Use float as real type, default is double. Must be same in application & lib pro-file.
# CONFIG += mDnsIntern     # Use internal mDNS code for zero-config (no external dependency)
ARN += server
ARN += discover
ARN += qml
QT -= gui

win32 {
    CONFIG += mDnsIntern
}

QMAKE_CXXFLAGS_WARN_ON = -Wall -Wno-deprecated-declarations -Wno-deprecated-copy

include(src/ArnLib.pri)

!mDnsIntern {
    win32:CONFIG(release, debug|release): LIBS +=  -ldns_sd
    else:win32:CONFIG(debug, debug|release): LIBS +=  -ldns_sd
    else:unix: LIBS += -ldns_sd
}

win32 {
} else {
#DEFINES += DUMMY=\\\"$$system(rm build/ArnM.o)\\\"
}

greaterThan(QT_MAJOR_VERSION, 5) {
    TARGET = Arn6
} else {
    greaterThan(QT_MAJOR_VERSION, 4) {
        TARGET = Arn5
    } else {
        TARGET = Arn4
    }
}
TEMPLATE = lib

!win32:VERSION = $$ARNLIBVER

# CONFIG += staticlib
DEFINES += ARNLIB_LIBRARY
OBJECTS_DIR = build
MOC_DIR = build
RCC_DIR = build

OTHER_FILES += \
    arnlib.prf \
    doc/Internals.md \
    README.md \
    doc/Description.md \
    examples/Examples.txt \
    doc/Install.md \
    doc/Changelog_Todo.md


### Install
QMFEATURES=$$[QMAKEFEATURES]
isEmpty(QMFEATURES) {
    win32: QMFEATURES = $$OUT_PWD/../qtfeatures
    else:  QMFEATURES = /usr/include/qtfeatures
}
win32 {
    headers.path = $$OUT_PWD/../include/ArnInc
    target.path = $$OUT_PWD/../lib
} else {
    headers.path = /usr/include/ArnInc
    exists(/usr/lib64) {
        target.path = /usr/lib64
    } else {
        target.path = /usr/lib
    }
}
features.path   = $$QMFEATURES
features.files += arnlib.prf
headers.files  += src/ArnInc/* arnlib.prf
greaterThan(QT_MAJOR_VERSION, 4) {
    Txt = "$$LITERAL_HASH Do not change! Autogenerated from ArnLib."
    write_file(arnlib_conf.pri, Txt)
    contains(ARN, scriptjs) {
        Txt = "contains(ARN, scriptauto): ARN += scriptjs"
        write_file(arnlib_conf.pri, Txt, append)
    }
    contains(ARN, script) {
        Txt = "contains(ARN, scriptauto): ARN += script"
        write_file(arnlib_conf.pri, Txt, append)
    }
    ArnRealFloat {
        Txt = "CONFIG += ArnRealFloat"
        write_file(arnlib_conf.pri, Txt, append)
    }
    mDnsIntern {
        Txt = "CONFIG += mDnsIntern"
        write_file(arnlib_conf.pri, Txt, append)
    }
    headers.files  += arnlib_conf.pri
    features.files += arnlib_conf.pri
}
INSTALLS += \
    target \
    headers \
    features


### Custom target 'doc' in *.pro file

doc.commands = doxygen Doxyfile
doc.depends = FORCE
QMAKE_EXTRA_TARGETS += doc
QMAKE_DISTCLEAN += doc/doxy_html/* doc/doxy_html/search/*
